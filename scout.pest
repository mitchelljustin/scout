//
// Created by intellij-pest on 2023-07-13
// scout
// Author: mitch
//

program = {
    SOI ~
    stmts ~
    EOI
}

stmts = {
    (stmt ~ NEWLINE)*
}

stmt = {
    func_def |
    var_def |
    mod_def |
    expr
}

mod_def = {
    "mod" ~ path ~ stmts ~ "end"
}

expr = {
    multiline_expr |
    call |
    literal |
    var_ref
}

var_ref = { ident }

multiline_expr = {
    "do" ~ NEWLINE ~ ((stmt | return_stmt) ~ NEWLINE)* ~ "end"
}

return_stmt = {
    "return" ~ expr
}

func_def = {
    "fn" ~ ident ~ "(" ~ param_list ~ ")" ~ "=" ~ expr
}

var_def = {
    ident ~ "=" ~ expr
}

call = {
    path ~ "(" ~ arg_list ~ ")"
}

path = {
    ident ~ ("::" ~ ident)*
}

binary_op = {
    "+"
}

param_list = {
    (ident ~ ("," ~ ident)*) | ""
}

arg_list = {
    (expr ~ ("," ~ expr)*) | ""
}

literal = {
    number | string | bool | nil
}

bool = @{ "true" | "false" }
nil = @{ "nil" }

KEYWORD = @{ "fn" | "do" | "end" | "mod" }

ident = @{
    !(KEYWORD ~ !XID_START) ~
    XID_START ~ XID_CONTINUE*
}

number = @{
    "-"? ~
    integer ~
    ("." ~ mantissa)?
}
integer = _{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = _{ ASCII_DIGIT+ }

string = @{
    "\"" ~ string_inner ~ "\""
}

string_inner = ${(!"\"" ~ ANY)*}

WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
