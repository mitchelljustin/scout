//
// Created by intellij-pest on 2023-07-13
// scout
// Author: mitch
//

program = {
    SOI ~
    (expr ~ NEWLINE)* ~
    EOI
}

expr = {
    multiline_expr |
    func_def |
    var_def |
    call |
    literal |
    ident
}

multiline_expr = {
    "do" ~ NEWLINE ~ (expr ~ NEWLINE)* ~ "end"
}

func_def = {
    "fn" ~ path ~ "(" ~ param_list ~ ")" ~ "=" ~ expr
}

var_def = {
    ident ~ "=" ~ expr
}

call = {
    path ~ "(" ~ arg_list ~ ")"
}

path = {
    ident ~ ("::" ~ ident)*
}

binary_op = {
    "+"
}

param_list = {
    (ident ~ ("," ~ ident)*) | ""
}

arg_list = {
    (expr ~ ("," ~ expr)*) | ""
}

literal = {
    number | string | bool | nil
}

bool = @{ "true" | "false" }
nil = @{ "nil" }

KEYWORD = @{ "fn" | "do" | "end" }

ident = @{
    !(KEYWORD ~ !XID_START) ~
    XID_START ~ XID_CONTINUE*
}

number = @{
    "-"? ~
    integer ~
    ("." ~ mantissa)?
}
integer = _{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = _{ ASCII_DIGIT+ }

string = @{
    "\"" ~ string_inner ~ "\""
}

string_inner = ${(!"\"" ~ ANY)*}

WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
